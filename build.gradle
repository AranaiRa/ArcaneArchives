buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath "com.wynprice.cursemaven:CurseMaven:1.1.0"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.wynprice.cursemaven'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = "0.2.0.15"
group = "com.aranairra.arcanearchives"
archivesBaseName = "arcanearchives"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = "${forge_mappings}"
    if (file('../run').exists()) {
        runDir = "../run"
    } else {
        runDir = "run"
    }

    useDepAts = true
    coreMod = "com.aranaira.arcanearchives.core.AALoadingPlugin"

    replace "GRADLE:VERSION", project.version
}

configurations {
    shade
    compile.extendsFrom shade
}

configurations {
    embed
    embed.transitive = false
}

repositories {
    maven {
        name = "JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "HWYLA"
        url = "http://tehnut.info/maven"
    }
    maven { // The One Probe
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven { // Quark and autorreglib
        name = "BlameJared"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {  // guidebook common dep
        name = "guidebook"
        url = "http://dogforce-games.com/maven"
    }
    maven { // guidebook transitive deps and baubles
        name = "mcmoddev"
        url = "https://maven.mcmoddev.com"
    }
    flatDir {
        name = "local jars"
        dirs 'embed'
    }
    flatDir {
        name = "thaumcraft jars"
        dirs 'thaumcraft'
    }
    maven {
        name = "HellfirePVP"
        url = "https://maven.hellfiredev.net"
    }
    maven {
        name = "sponge"
        url = "http://repo.spongepowered.org/maven/"
    }
    maven {
        url = "http://maven.tterrag.com"
    }
}

dependencies {
    deobfProvided "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    compileOnly "mcp.mobius.waila:Hwyla:${project.hwyla_version}"
    compileOnly "mcjty.theoneprobe:TheOneProbe-${top_version}"
    compileOnly "vazkii.quark:Quark:${quark_version}:api"
    deobfCompile "com.azanor:Baubles:${version_baubles}"
    compileOnly "hellfirepvp.astralsorcery:astralsorcery:1.12.2-1.10.17-v124"
    compileOnly 'mouse-tweaks:MouseTweaks:2.10:mc1.12.2'
    compileOnly 'inventory-tweaks:InventoryTweaks:1.64+dev.146'

    // guidebook dependencies
    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"
    shade ("gigaherz.commons:gigaherz.commons-1.12.1:0.6.4") {
        transitive = false
    }

    deobfCompile("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${ct_version}") {
        exclude group: 'mezz.jei'
    }
    //deobfCompile "epicsquid.mysticallib:mysticallib:1.12.2-1.3.1-11"

    // modified guidebook, note "lirelent" package
    deobfCompile "gigaherz.lirelent.guidebook:Guidebook-1.12.2:2.9.1.s5"
    embed "gigaherz.lirelent.guidebook:Guidebook-1.12.2:2.9.1.s5"

    compileOnly 'thaumcraft:Thaumcraft:1.12.2:6.1.BETA26'
    compileOnly curse.resolve("electroblobs-wizardry", "2656626")
    compileOnly curse.resolve("potion-core", "2736248")

    shade("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    into('/META-INF/libraries') {
        from configurations.embed
    }

    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": "true",
                "FMLCorePlugin"              : "com.aranaira.arcanearchives.core.AALoadingPlugin",

                "MixinConfigs"               : "mixins.arcanearchives.json",
                "TweakClass"                 : "org.spongepowered.asm.launch.MixinTweaker",
                "ForceLoadAsMod"             : "true",

                "FMLAT"                      : "aa_at.cfg",

                "ContainedDeps"              : configurations.embed.collect { it.getName() }.join(' ')
        ])
    }

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'

            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.MF'

            exclude '**/*.html'
        }
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

task apiJar(type: Jar) {
    from (sourceSets.main.allJava)
    from (sourceSets.main.output)
    include 'com/aranaira/arcanearchives/api/**'

    classifier = 'api'

    archiveName = "${baseName}-${version}-api.${extension}"
}

task srcJar(type: Jar) {
    from (sourceSets.main.output)

    classifier = 'sources'

    archiveName = "${baseName}-${version}-sources.${extension}"
}

artifacts {
    archives srcJar, apiJar, deobfJar
}

mixin {
    add sourceSets.main, "mixins.arcanearchives.refmap.json"
}
